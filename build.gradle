plugins {
  id 'java'
  id 'com.google.cloud.tools.jib' version '3.2.0'
  id 'com.palantir.git-version' version '0.13.0'
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.1'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.1'

  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

  implementation 'com.google.guava:guava:23.6-jre'
}

def gitInfo = versionDetails()

jar {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  manifest {
    attributes(
      'Artifact-Id': archivesBaseName,
      'Revision' : gitInfo.gitHash[0..7],
      'Main-Class': 'example.Main'
    )
  }
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

jib {
  from {
    image = 'adoptopenjdk/openjdk11:alpine-jre'
    platforms {
      platform {
        architecture = 'amd64'
        os = 'linux'
      }
    }
  }
  to {
    image = 'ghcr.io/t2y/jib-sample'
    tags = ['mytag', 'test']
  }
  container {
    labels = [
      'org.opencontainers.image.description': 'jib sample code',
      'org.opencontainers.image.licenses': 'Apache-2.0',
      'org.opencontainers.image.source': 'https://github.com/t2y/jib-sample',
      'org.opencontainers.image.title': 'jib-sample',
      'org.opencontainers.image.url': 'https://github.com/t2y/jib-sample'
    ]
    format = 'OCI'
  }
}

